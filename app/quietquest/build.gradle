plugins {
  id 'java'
  id 'application'
  id 'org.javamodularity.moduleplugin' version '1.8.15'
  id 'org.openjfx.javafxplugin' version '0.1.0'
  id 'org.beryx.jlink' version '3.0.1'
}

java {
  toolchain {
    languageVersion.set(JavaLanguageVersion.of(21))
  }
}

jar {
  manifest {
    attributes(
            'Main-Class': 'quietquest.Launcher'
    )
  }
}

task fatJar(type: Jar) {
  archiveClassifier.set('fat')
  duplicatesStrategy = DuplicatesStrategy.EXCLUDE
  manifest {
    attributes(
            'Main-Class': 'quietquest.Launcher'
    )
  }
  from {
    configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
  }
  with jar
}

group 'quiet-quest'
version '1.0-SNAPSHOT'

repositories {
  mavenCentral()
}

ext {
  junitVersion = '5.10.2'
}

tasks.withType(JavaCompile).configureEach {
  options.encoding = 'UTF-8'
}

application {
  mainModule = 'quietquest'
  mainClass = 'quietquest.QuietQuestMain'
}

javafx {
  version = '21.0.2'
  modules = ['javafx.controls', 'javafx.fxml', 'javafx.media']
}

dependencies {
  implementation 'com.hivemq:hivemq-mqtt-client:1.2.1'
  implementation group: 'org.postgresql', name: 'postgresql', version: '42.7.3'
  implementation group: 'org.openjfx', name: 'javafx-media', version: '11.0.2'

  testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
  testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")
}

test {
  useJUnitPlatform()
}

jlink {
  imageZip = project.file("${buildDir}/distributions/app-${javafx.platform.classifier}.zip")
  options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
  launcher {
    name = 'app'
  }
}

jlinkZip {
  group = 'distribution'
}
